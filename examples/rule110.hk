//
// rule110.hk
//

let size = 100;
var prev = [];
var i;

i = 0;
while (i < size - 1) {
  prev[] = false;
  i++;
}
prev[] = true;

fn calc(p, i) {
  let prev = p[i - 1];
  let curr = p[i];
  let next = p[i + 1];
  if (prev && curr && next) {
    return false;
  }
  if (prev && curr && !next) {
    return true;
  }
  if (prev && !curr && next) {
    return true;
  }
  if (prev && !curr && !next) {
    return false;
  }
  if (!prev && curr && next) {
    return true;
  }
  if (!prev && curr && !next) {
    return true;
  }
  if (!prev && !curr && next) {
    return true;
  }
  return false;
}

i = 0;
while (i < size) {
  var line = [false];
  var j;
  j  = 1;
  while (j < size - 1) {
    line[] = calc(prev, j);
    j++;
  }
  line[] = false;
  var output = "";
  j = 0;
  while (j < size) {
    output += if (line[j]) "*" else " ";
    j++;
  }
  println(output);
  prev = line;
  i++;
}
